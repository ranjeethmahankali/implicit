#define UINT32_TYPE uint
#define UINT8_TYPE uchar
#define FLT_TYPE float

#define PACKED __attribute__((packed))

#include "primitives.clh"

#undef UINT_TYPE
#undef FLT_TYPE

#define CAST_TYPE(type, name, ptr) global type* name = (global type*)ptr

float f_box(global uchar* packed,
            float3* pt)
{
  CAST_TYPE(i_box, box, packed);
  global float* bounds = box->bounds;
  return
    length((float3)(max(0.0f, fabs((*pt).x - bounds[0]) - bounds[3]),
                    max(0.0f, fabs((*pt).y - bounds[1]) - bounds[4]),
                    max(0.0f, fabs((*pt).z - bounds[2]) - bounds[5]))) -
    min(min(max(0.0f, bounds[3] - fabs((*pt).x - bounds[0])),
            max(0.0f, bounds[4] - fabs((*pt).y - bounds[1]))),
        max(0.0f, bounds[5] - fabs((*pt).z - bounds[2])));
}

float f_sphere(global uchar* ptr,
               float3* pt)
{
  CAST_TYPE(i_sphere, sphere, ptr);
  // Vector from the center to the point.
  return length(*pt - (float3)(sphere->center[0],
                               sphere->center[1],
                               sphere->center[2])) - fabs(sphere->radius);
}

float f_cylinder(global uchar* ptr,
                 float3* pt)
{
  CAST_TYPE(i_cylinder, cyl, ptr);
  float3 p1 = (float3)(cyl->point1[0],
                       cyl->point1[1],
                       cyl->point1[2]);
  float3 p2 = (float3)(cyl->point2[0],
                       cyl->point2[1],
                       cyl->point2[2]);
  float3 ln = p2 - p1;
  float halfLen = length(ln) * 0.5f;
  ln /= halfLen * 2.0f;
  float3 r = (*pt) - ((p1 + p2) * 0.5f);
  float y = length(r - ln * dot(ln, r));
  float x = fabs(dot(ln, r));

  return length((float2)(max(0.0f, x - halfLen),
                         max(0.0f, y - cyl->radius))) -
    min(max(0.0f, cyl->radius - y), max(0.0f, halfLen - x));
}

float f_gyroid(global uchar* ptr,
               float3* pt)
{
  CAST_TYPE(i_gyroid, gyroid, ptr);
  float scale = gyroid->scale;
  float thick = gyroid->thickness;
  float sx, cx, sy, cy, sz, cz;
  sx = sincos((*pt).x * scale, &cx);
  sy = sincos((*pt).y * scale, &cy);
  sz = sincos((*pt).z * scale, &cz);
  float factor = 4.0f / thick;
  float fval = (sx * cy + sy * cz + sz * cx) / factor;
  float result = fabs(fval) - (thick / factor);
  return result;
}

float f_schwarz(global uchar* ptr,
                float3* pt)
{
  CAST_TYPE(i_schwarz, lattice, ptr);
  float factor = 4.0f / lattice->thickness;
  float cx = cos((*pt).x * lattice->scale);
  float cy = cos((*pt).y * lattice->scale);
  float cz = cos((*pt).z * lattice->scale);
  float result = fabs((cx + cy + cz) / factor) - (lattice->thickness / factor);
  return result;
}

float f_halfspace(global uchar* ptr,
                  float3* pt)
{
  CAST_TYPE(i_halfspace, hspace, ptr);
  float3 origin = (float3)(hspace->origin[0],
                           hspace->origin[1],
                           hspace->origin[2]);
  float3 normal = normalize((float3)(hspace->normal[0],
                                     hspace->normal[1],
                                     hspace->normal[2]));

  return dot((*pt) - origin, -normal);
}

float f_polyface(global uchar* ptr,
                 float3* pt)
{
  global uint* uptr = (global uint*)ptr;
  uint nVerts = *uptr;

  if (nVerts == 0)
    return 1.0f; // Always outside.
  if (nVerts > 100) // Too many. Not supported.
    return 1.0f;
  
  global float* coords = (global float*)(uptr + 1);
  float wsum = 0.0f, dsum = 0.0f;
  for (uint i0 = 0; i0 < 1; i0++) {
    uint i1 = ((i0 + nVerts) - 1) % nVerts;
    uint i2 = (i0 + 1) % nVerts;
      
    float3 v1 = (float3)(*(coords + 3 * i1), *(coords + 3 * i1 + 1), *(coords + 3 * i1 + 2));
    float3 v0 = (float3)(*(coords + 3 * i0), *(coords + 3 * i0 + 1), *(coords + 3 * i0 + 2));
    float3 v2 = (float3)(*(coords + 3 * i2), *(coords + 3 * i2 + 1), *(coords + 3 * i2 + 2));

    float3 norm = normalize(cross(v2 - v0, v1 - v0));
    float d = dot(norm, (*pt) - v0);
    float w = length((*pt) - v0);
    if (w == 0) {
      return d;
    }
    w = 1.0f / w;
    wsum += w;
    dsum = d * w;
  }

  return dsum / wsum;
}

float f_simple(global uchar* ptr,
               uchar type,
               float3* pt
#ifdef CLDEBUG
               , uchar debugFlag
#endif
               )
{
  switch (type){
  case ENT_TYPE_BOX: return f_box(ptr, pt);
  case ENT_TYPE_SPHERE: return f_sphere(ptr, pt);
  case ENT_TYPE_GYROID: return f_gyroid(ptr, pt);
  case ENT_TYPE_SCHWARZ: return f_schwarz(ptr, pt);
  case ENT_TYPE_CYLINDER: return f_cylinder(ptr, pt);
  case ENT_TYPE_HALFSPACE: return f_halfspace(ptr, pt);
  case ENT_TYPE_POLYFACE: return f_polyface(ptr, pt);
  default: return 1.0f;
  }
}

float apply_union(float blend_radius,
                  float a,
                  float b,
                  float3* pt
#ifdef CLDEBUG
                  , uchar debugFlag
#endif
                  )
{
  if (a < blend_radius && b < blend_radius){
    return blend_radius - length((float2)(blend_radius - a, blend_radius - b));
  }
  else{
    return min(a, b);
  }
}

float apply_intersection(float blend_radius,
                         float a,
                         float b,
                         float3* pt
#ifdef CLDEBUG
                         , uchar debugFlag
#endif
                         )
{
  if (blend_radius == 0.0f){
    return max(a, b);
  }
  else if (a > -blend_radius && b > -blend_radius){
    return length((float2)(a + blend_radius, b + blend_radius)) - blend_radius;
  }
  else{
    return max(a, b);
  }
}

float apply_linblend(lin_blend_data op,
                     float a,
                     float b,
                     float3* pt
#ifdef CLDEBUG
                      , uchar debugFlag
#endif
                      )
{
  float3 p1 = (float3)(op.p1[0],
                       op.p1[1],
                       op.p1[2]);
  float3 ln = (float3)(op.p2[0],
                       op.p2[1],
                       op.p2[2]) - p1;
  float modL = length(ln);
  float lambda = min(1.0f, max(0.0f, dot((*pt) - p1, ln / (modL * modL))));
  float i = lambda * b + (1.0f - lambda) * a;
  return (i * modL) / sqrt(modL * modL + (a - b) * (a - b));
}

float apply_smoothblend(smooth_blend_data op,
                        float a,
                        float b,
                        float3* pt
#ifdef CLDEBUG
                         , uchar debugFlag
#endif
                         )
{
  float3 p1 = (float3)(op.p1[0],
                       op.p1[1],
                       op.p1[2]);
  float3 ln = (float3)(op.p2[0],
                       op.p2[1],
                       op.p2[2]) - p1;
  float modL = length(ln);
  float lambda = min(1.0f, max(0.0f, dot((*pt) - p1, ln / (modL * modL))));
  lambda = 1.0f / (1.0f + pow(lambda / (1.0f - lambda), -2.0f));
  float i = lambda * b + (1.0f - lambda) * a;
  return (i * modL) / sqrt(modL * modL + (a - b) * (a - b)) * 0.8;
}

float apply_op(op_defn op,
               float a,
               float b,
               float3* pt
#ifdef CLDEBUG
                      , uchar debugFlag
#endif
                )
{
  switch(op.type){
  case OP_NONE: return a;
  case OP_UNION: return apply_union(op.data.blend_radius,
                                    a, b, pt
#ifdef CLDEBUG
                                    , debugFlag
#endif
                                    );
  case OP_INTERSECTION: return apply_intersection(op.data.blend_radius,
                                                  a, b, pt
#ifdef CLDEBUG
                                                  , debugFlag
#endif
                                                  );
  case OP_SUBTRACTION: return apply_intersection(op.data.blend_radius,
                                                  a, -b, pt
#ifdef CLDEBUG
                                                  , debugFlag
#endif
                                                  );
  case OP_OFFSET: return a - op.data.offset_distance;
  case OP_LINBLEND: return apply_linblend(op.data.lin_blend, a, b, pt
#ifdef CLDEBUG
                      , debugFlag
#endif
                                          );
  case OP_SMOOTHBLEND: return apply_smoothblend(op.data.smooth_blend, a, b, pt
#ifdef CLDEBUG
                      , debugFlag
#endif
                                                );
  default: return a;
  }
}

float f_entity(global uchar* packed,
                global uint* offsets,
                global uchar* types,
                local float* valBuf,
                local float* regBuf,
                uint nEntities,
                global op_step* steps,
                uint nSteps,
                float3* pt
#ifdef CLDEBUG
                      , uchar debugFlag
#endif
                )
{
  /* printf("Number of entities: %u\n", nEntities); */
  if (nSteps == 0){
    if (nEntities > 0)
      return f_simple(packed, *types, pt
#ifdef CLDEBUG
                      , debugFlag
#endif
                      );
    else
      return 1.0f;
  }

  uint bsize = get_local_size(0);
  uint bi = get_local_id(0);
  // Compute the values of simple entities.
  for (uint ei = 0; ei < nEntities; ei++){
    valBuf[ei * bsize + bi] =
      f_simple(packed + offsets[ei], types[ei], pt
#ifdef CLDEBUG
               , debugFlag
#endif
               );
  }

  // Perform the csg operations.
  for (uint si = 0; si < nSteps; si++){
    uint i = steps[si].left_index;
    float l = steps[si].left_src == SRC_REG ?
      regBuf[i * bsize + bi] :
      valBuf[i * bsize + bi];
    
    i = steps[si].right_index;
    float r = steps[si].right_src == SRC_REG ?
      regBuf[i * bsize + bi] :
      valBuf[i * bsize + bi];
    
    regBuf[steps[si].dest * bsize + bi] =
      apply_op(steps[si].op, l, r, pt
#ifdef CLDEBUG
                      , debugFlag
#endif
               );
  }
  
  return regBuf[bi];
}
